buildscript {
    repositories {
        maven { url "http://repo.springsource.org/plugins-release" }
    }
    dependencies {
        classpath("org.gradle.api.plugins:gradle-tomcat-plugin:0.9.8")
    }
}

apply plugin: 'war'
apply plugin: 'tomcat'
apply plugin: 'java'

ext.slf4jVersion = '1.7.5'
ext.springSecurityVersion = "3.2.4.RELEASE"
ext.springVersion = "4.0.5.RELEASE"

group = 'org.springframework.security'

war.baseName = '/'

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.springframework') {
            details.useVersion springVersion
        }
    }
}

dependencies {

    def tomcatVersion = '7.0.54'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }

    providedCompile "javax.servlet:javax.servlet-api:3.0.1",
            'javax.servlet.jsp:jsp-api:2.1'

    compile "org.springframework.security:spring-security-config:$springSecurityVersion",
            "org.springframework.security:spring-security-web:$springSecurityVersion",
            "org.springframework:spring-webmvc:$springVersion",
            "org.springframework:spring-jdbc:$springVersion",
            "com.h2database:h2:1.3.168",
            "org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.0.Final",
            "org.hibernate:hibernate-entitymanager:3.6.10.Final",
            "org.slf4j:slf4j-api:$slf4jVersion",
            "org.slf4j:log4j-over-slf4j:$slf4jVersion",
            "org.slf4j:jul-to-slf4j:$slf4jVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "javax.servlet:jstl:1.2",
            "javax.validation:validation-api:1.0.0.GA",
            "org.hibernate:hibernate-validator:4.2.0.Final",
            "org.springframework.data:spring-data-jpa:1.3.4.RELEASE",
            "org.thymeleaf:thymeleaf-spring4:2.1.2.RELEASE",
            "org.thymeleaf.extras:thymeleaf-extras-tiles2-spring4:2.1.1.RELEASE"

    runtime 'cglib:cglib-nodep:2.2.2',
            'ch.qos.logback:logback-classic:0.9.30'

    testCompile 'junit:junit:4.7',
            "org.springframework:spring-test:$springVersion",
            'org.easytesting:fest-assert:1.4'
}

task wrapper(type: Wrapper) { gradleVersion = '1.11' }

tomcatRunWar.contextPath = ''